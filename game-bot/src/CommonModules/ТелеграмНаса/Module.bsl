#Область ПрограммныйИнтерфейс
//ВХОДНЫЕ ДАННЫЕ: Бот получает на вход данные в виде даты в формате "2023-10-20". Дата должна быть в диапазоне 
//от 1995-06-16 до сегодняшнего дня
//
// Выполняет обработку сообщения телеграм в выбранном режиме.
// 
// Параметры:
//  ДанныеЭлемента - Структура - Сообщение, полученное из телеграм
//  
Процедура ПриОбработкеЭлементаОчереди(ДанныеЭлемента) Экспорт
	
	Если ДанныеЭлемента.Свойство("message") Тогда
		
		Сообщение = ДанныеЭлемента.message;
		
		Если Не Сообщение.Свойство("from") Тогда
			Возврат;
		КонецЕсли;
		
		Если Не Сообщение.Свойство("text") Тогда
			Возврат;
		КонецЕсли;
		
		Текст = Сообщение.text;  
		
		Отправитель = Сообщение.from.id;
		
		РезультатПолученияФото = ДанныеНасаФото(Текст);
		
		Если РезультатПолученияФото.Успешно Тогда
			ИнтеграцияТелеграм.ОтправитьКартинку(
				Отправитель, 
				РезультатПолученияФото.ДвоичныеДанные);
				
				ИнтеграцияТелеграм.ОтправитьСообщение(
				Отправитель, 
				РезультатПолученияФото.ОписаниеФото);
		Иначе
			ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, РезультатПолученияФото.ТекстОшибки);
		КонецЕсли;	
			
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеНасаФото(Текст)
	
	Результат = Новый Структура;
	Результат.Вставить("Успешно", Истина);
	Результат.Вставить("ДвоичныеДанные", Неопределено);
	Результат.Вставить("ОписаниеФото", "");
	Результат.Вставить("ТекстОшибки", "");	
	
	Токен = Константы.ТокенСервисаПоискаФото.Получить();
	
	Если Не ЗначениеЗаполнено(Токен) Тогда
		Результат.ТекстОшибки = "Произошла внутреннняя ошибка, обратитесь к администратору бота @karisha281";
		Результат.Успешно = Ложь;
		ЗаписьЖурналаРегистрации("ОшибкаРаботыБота.СлучайноеФото", УровеньЖурналаРегистрации.Ошибка,,,
			"Заполните токен для работы с сервисом");
		Возврат Результат;
	КонецЕсли;	
	
	Соединение = Новый HTTPСоединение("api.nasa.gov",,,,,, Новый ЗащищенноеСоединениеOpenSSL); 

	АдресРесурса = СтрШаблон("/planetary/apod?api_key=%1&date=%2", Токен, Формат(Текст, "ДФ=yyyy-MM-dd"));		
	Запрос = Новый HTTPЗапрос(АдресРесурса);
	
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		Результат.ТекстОшибки = "Произошла внутреннняя ошибка, обратитесь к администратору бота @karisha281";
		Результат.Успешно = Ложь;
		ЗаписьЖурналаРегистрации("ОшибкаРаботыБота.СлучайноеФото", УровеньЖурналаРегистрации.Ошибка,,,
			"Ошибка вызова сервиса");
		Возврат Результат;
	КонецЕсли;
		
	СтрокаJSON = Ответ.ПолучитьТелоКакСтроку();                   
	
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(СтрокаJSON);
	ДанныеОтвета = ПрочитатьJSON(Чтение, Истина);
	Чтение.Закрыть();
	
	СсылкаНаФото = ДанныеОтвета["url"];
	Результат.ОписаниеФото = ДанныеОтвета["explanation"];
	
	Попытка
		Результат.ДвоичныеДанные = ПолучитьДанныеФото(СсылкаНаФото);
	Исключение
		Результат.ТекстОшибки = "Произошла внутреннняя ошибка, обратитесь к администратору бота @karisha281";
		Результат.Успешно = Ложь;
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ОшибкаРаботыБота.СлучайноеФото", УровеньЖурналаРегистрации.Ошибка,,,
			"Ошибка скачивания картинки: " + ПодробноеПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Возврат Результат; 

КонецФункции

Функция ПолучитьДанныефото(СсылкаНаФото)
	
	СсылкаБезПротокола = Сред(СсылкаНаФото, 9);
	
	НомерПервогоСлеша = СтрНайти(СсылкаБезПротокола, "/");
	
	АдресСервера = Лев(СсылкаБезПротокола, НомерПервогоСлеша - 1);
	АдресРесурсаНаСервере = Сред(СсылкаБезПротокола, НомерПервогоСлеша);
	
	Соединение = Новый HTTPСоединение(АдресСервера,,,,,, Новый ЗащищенноеСоединениеOpenSSL);		
	Запрос = Новый HTTPЗапрос(АдресРесурсаНаСервере);
	
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение "Ошибка скачивания фото, код состояния: " + Ответ.КодСостояния;
	КонецЕсли;
	
	Возврат Ответ.ПолучитьТелоКакДвоичныеДанные();
	
КонецФункции

#КонецОбласти

