
#Область ПрограммныйИнтерфейс

// Выполняет обработку сообщения телеграм в выбранном режиме.
// 
// Параметры:
//  ДанныеЭлемента - Структура - Сообщение, полученное из телеграм
Процедура ПриОбработкеЭлементаОчереди(ДанныеЭлемента) Экспорт

	Если ДанныеЭлемента.Свойство("message") Тогда

		Сообщение = ДанныеЭлемента.message;

		Если Не Сообщение.Свойство("from") Тогда
			Возврат;
		КонецЕсли;

		Если Не Сообщение.Свойство("text") Тогда
			Возврат;
		КонецЕсли;

		Отправитель = Сообщение.from.id;

		РезультатПолученияСсылки = ДанныеПогодыАрх();

		Если РезультатПолученияСсылки.Успешно Тогда
			ИнтеграцияТелеграм.ОтправитьСообщение(
				Отправитель,
				РезультатПолученияСсылки.ТекстСообщения);
		Иначе
			ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, РезультатПолученияСсылки.ТекстОшибки);
		КонецЕсли;	

	КонецЕсли;	

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеПогодыАрх()

	Результат = Новый Структура;
	Результат.Вставить("Успешно", Истина);
	Результат.Вставить("ТекстСообщения", "");
	Результат.Вставить("ТекстОшибки", "");	

	Токен = Константы.ТокенСервисаПогодыАрх.Получить();

	Если Не ЗначениеЗаполнено(Токен) Тогда
		Результат.ТекстОшибки = "Произошла внутреннняя ошибка, обратитесь к администратору бота @mazytka7";
		Результат.Успешно = Ложь;
		ЗаписьЖурналаРегистрации("ОшибкаРаботыБота.ПогодаАрх", УровеньЖурналаРегистрации.Ошибка,,,
			"Заполните токен для работы с сервисом");
		Возврат Результат;
	КонецЕсли;	

	Соединение = Новый HTTPСоединение("api.weather.yandex.ru",,,,,, Новый ЗащищенноеСоединениеOpenSSL);


	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-Yandex-API-Key", Токен);

	Запрос = Новый HTTPЗапрос("v2/informers?lat=64.5401&lon=40.5433", Заголовки);

	Ответ = Соединение.Получить(Запрос);

	Если Ответ.КодСостояния <> 200 Тогда
		Результат.ТекстОшибки = "Произошла внутреннняя ошибка, обратитесь к администратору бота @mazytka7";
		Результат.Успешно = Ложь;
		ЗаписьЖурналаРегистрации("ОшибкаРаботыБота.ПогодаАрх", УровеньЖурналаРегистрации.Ошибка,,,
			"Ошибка вызова сервиса");
		Возврат Результат;
	КонецЕсли;

	СтрокаJSON = Ответ.ПолучитьТелоКакСтроку();

	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(СтрокаJSON);
	ДанныеОтвета = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();

	ТемператураАрх = ДанныеОтвета.fact.temp;
	Ощущается = ДанныеОтвета.fact.feels_like;
	Дата = ТекущаяДатаСеанса();
	ПодробнаяИнформация = ДанныеОтвета.info.url;
	
	Результат.ТекстСообщения = СтрШаблон("Сегодня %1 в Архангельске:
	|Температура: %2 °С;
	|Ощущается как: %3 °С;
	|Подробная информация на сайте: %4", Формат(Дата, "ДФ=dd.MM.yyyy"), ТемператураАрх, Ощущается, ПодробнаяИнформация);

	Возврат Результат;

КонецФункции


#КонецОбласти