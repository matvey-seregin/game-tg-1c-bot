
&НаКлиенте
Процедура ПолучитьФото(Команда)
	
	ПолучитьФотоНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьФотоНаСервере()
	
	Соединение = Новый HTTPСоединение("api.unsplash.com",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	
	Токен = Константы.ТокенСервисаПоискаФото.Получить();
	
	Если Не ЗначениеЗаполнено(Токен) Тогда
		ВызватьИсключение "Заполните токен для работы с сервисом";
	КонецЕсли;
	
	ШаблонЗаголовка = "Client-ID %1";
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Authorization", СтрШаблон(ШаблонЗаголовка, Токен));
	
	Запрос = Новый HTTPЗапрос("/photos/random", Заголовки);
	
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение "Ошибка вызова сервиса";
	КонецЕсли;
	
	СтрокаJSON = Ответ.ПолучитьТелоКакСтроку();
	
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(СтрокаJSON);
	ДанныеОтвета = ПрочитатьJSON(Чтение, Истина);
	Чтение.Закрыть();
	
	СсылкаНаФото = ДанныеОтвета["urls"]["regular"];
	ОписаниеФото = ДанныеОтвета["alt_description"];
	
	ПолучитьДанныеФото(СсылкаНаФото);
	
	ОтправитьФотоВсемПодписчикам(ОписаниеФото);

КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеФото(СсылкаНаФото)
	
	СсылкаБезПротокола = Сред(СсылкаНаФото, 9);
	
	НомерПервогоСлеша = СтрНайти(СсылкаБезПротокола, "/");
	
	АдресСервера = Лев(СсылкаБезПротокола, НомерПервогоСлеша - 1);
	АдресРесурсаНаСервере = Сред(СсылкаБезПротокола, НомерПервогоСлеша);
	
	Соединение = Новый HTTPСоединение(АдресСервера,,,,,, Новый ЗащищенноеСоединениеOpenSSL);		
	Запрос = Новый HTTPЗапрос(АдресРесурсаНаСервере);
	
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение "Ошибка скачивания фото";
	КонецЕсли;
	
	ДанныеФото = Ответ.ПолучитьТелоКакДвоичныеДанные();
	
	АдресФото = ПоместитьВоВременноеХранилище(ДанныеФото, УникальныйИдентификатор);	
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьФотоВсемПодписчикам(ОписаниеФото)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчетныеЗаписиТелеграм.Код
		|ИЗ
		|	Справочник.УчетныеЗаписиТелеграм КАК УчетныеЗаписиТелеграм";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ИнтеграцияТелеграм.ОтправитьКартинку(
			Выборка.Код, 
			ПолучитьИзВременногоХранилища(АдресФото), 
			ОписаниеФото);
		
	КонецЦикла;
	
КонецПроцедуры
